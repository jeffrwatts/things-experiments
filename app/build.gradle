apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'


android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "com.skiaddict.thingsexperiments"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['./assets']
        }
    }
}

// Download model zip file into ../../assets directory
// unzip it to demo project's own ./assets directory
import de.undercouch.gradle.tasks.download.Download
task downloadFile(type: Download) {
    src 'https://storage.googleapis.com/download.tensorflow.org/models/inception5h.zip'
    dest projectDir.toString() + '/../../assets/inception.zip'
}
task unzip(type: Copy) {
    from zipTree(projectDir.toString() + '/../../assets/inception.zip')
    into file(projectDir.toString() + '/assets')
}
unzip.dependsOn downloadFile

project.afterEvaluate {
    if (!(new File(projectDir.toString() + '/assets')).exists()) {
        preBuild.dependsOn unzip
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    provided 'com.google.android.things:androidthings:0.5.1-devpreview'
    // GPIO Button Driver.
    compile 'com.google.android.things.contrib:driver-button:0.3'
    // Temp & Pressure Sensor.
    compile 'com.google.android.things.contrib:driver-bmx280:0.3'
    // Accelerometer
    compile 'com.google.android.things.contrib:driver-mma7660fc:0.2'
    // GPS
    compile 'com.google.android.things.contrib:driver-gps:0.3'
    // RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // TensorFlow
    compile 'org.tensorflow:tensorflow-android:1.2.0'
}
